plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.lian.marketing'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation 'org.mockito:mockito-core:5.14.1'
    //Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // Mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
    //OPENAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    //implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
    // Netflix Eureka
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.2.1'
    // AWS S3
    implementation 'com.amazonaws:aws-java-sdk:1.12.774'

    //Vulneravities
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.3'
    implementation 'org.springframework:spring-web:6.2.8'
    implementation 'net.minidev:json-smart:2.5.2'
    implementation 'io.netty:netty-handler:4.1.118.Final'
    implementation 'io.netty:netty-common:4.1.118.Final'
    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.8'
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'
    implementation 'org.springframework.security:spring-security-crypto:6.4.4'
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.apache.httpcomponents.client5' &&
                details.requested.name == 'httpclient5') {
            details.useVersion '5.4.3'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacocoHtml')
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect{
                    fileTree(dir: it, excludes: [
                            "**/mapper/**",
                            "**/dto/**",
                            "**/infrastructure/**",
                            "**/handler/**",
                            "**/configuration/**"
                    ])
                })
        )
    }
}

jacoco {
    toolVersion = "0.8.12"
    reportsDirectory = layout.buildDirectory.dir('reports/jacocoHtml')
}
